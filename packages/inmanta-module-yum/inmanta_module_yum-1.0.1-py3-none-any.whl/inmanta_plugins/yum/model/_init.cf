"""
    Copyright 2024 Inmanta

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    Contact: code@inmanta.com
"""
import fs

entity Repository:
    """
        A yum repository.

        Constraint: The attributes baseurl, mirrorlist and metalink cannot be null at the same time.
    """
    string name
    bool gpgcheck=false
    bool enabled=true
    string? baseurl=null
    string? mirrorlist=null
    string? metalink=null
    string gpgkey=""
    number metadata_expire=7200
    bool skip_if_unavailable=false
end

std::Host.repos [0:] -- Repository.host [1]

implementation validateInput for Repository:
   std::assert(
       expression=self.baseurl != null or self.mirrorlist != null or self.metalink != null,
       message="baseurl, mirrorlist and metalink cannot be null at the same time.",
   )
end

implementation redhatRepo for Repository:
    repo = fs::File(
        mode=644,
        owner="root",
        group="root",
        path="/etc/yum.repos.d/{{ name }}.repo",
        content=std::template("yum/repo.tmpl"),
        host=host,
        provides=self.provides
    )
end

implement Repository using validateInput
implement Repository using redhatRepo when std::familyof(host.os, "redhat")


entity Package extends std::Resource:
    """
        A software package installed on a managed device.

        :attr name: The name of the package to manage
        :attr state: The state of the package. Valid values are 'installed', 'removed' or 'latest'.
                     latest will upgrade the package when an update is available.
    """
    string name
    std::package_state state
    bool send_event=true
end

implement Package using std::none 

Package.host [1] -- std::Host

index Package(host, name)


entity Packages:
    """ Define the state for multiple packages at once

        :param name: A list of package names
        :param state: The state of the package
    """
    string[] name
    std::package_state state="installed"
end

Packages.host [1] -- std::Host

implement Packages using pkgs

implementation pkgs for Packages:
    for pkg in name:
        Package(host=self.host, name=pkg, state=self.state, requires=self.requires, provides=self.provides)
    end
end
